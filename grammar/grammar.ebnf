start: instruction+

instruction: canvas | stroke | brush | sample | assignment | COMMENT | rightbracecommand | save
stroke: STROKECOMMAND value value value value [curve] [wavy] -> stroke
brush: BRUSHCOMMAND (brushvalue)+  -> brush
canvas: CANVASCOMMAND value value value
save: SAVECOMMAND CNAME -> save
assignment: variable "=" value -> assignment
sample: SAMPLECOMMAND value (samplervalue)* -> sample

brushvalue: hue | sat | bright | alpha | thick | rough -> brushvalue
samplervalue: margin | left_margin | right_margin -> samplervalue


// brush properties
hue: "h" value -> hue
sat: "s" value -> sat
bright: "b" value -> bright
alpha: "a" value -> alpha
thick: "t" value -> thick
rough: "r" value -> rough

// sample properties
margin: "m" value -> margin
left_margin: "lm" value -> left_margin
right_margin: "rm" value -> right_margin

// path properties
curve: "c" value -> curve
wavy:  "w" value -> wavy

// braces
rightbracecommand: RIGHTBRACE -> rightbrace

value: NUMBER | ("(" NUMBER "," NUMBER")") | variable [multiplier]
variable: "{" CNAME "}"
multiplier: "*" NUMBER

CANVASCOMMAND: "canvas"
STROKECOMMAND: "stroke"
BRUSHCOMMAND: "brush"
SAMPLECOMMAND: "sample"
SAVECOMMAND: "save"
COMMENT: "-" /[^\n]*/ NEWLINE
RIGHTBRACE: "]"

%import common.SIGNED_NUMBER -> NUMBER
%import common.NEWLINE -> NEWLINE
%import common.INTEGER -> INTEGER
%import common.CNAME -> CNAME
%import common.WS
%ignore WS