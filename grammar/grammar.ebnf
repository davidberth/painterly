start: instruction+

instruction: canvas | stroke | brush | sample | assignment
| COMMENT | rightbracecommand | light | save
stroke: STROKECOMMAND value value value value [curve] [wavy] -> stroke
brush: BRUSHCOMMAND (brushvalue)+  -> brush
canvas: CANVASCOMMAND value value value value value value
save: SAVECOMMAND CNAME -> save
assignment: variable "=" value -> assignment
sample: SAMPLECOMMAND value (samplervalue)* -> sample
light: LIGHTCOMMAND value value value value value value value -> light

brushvalue: hue | sat | bright | alpha | thick | rough | consistency -> brushvalue
samplervalue: margin | left_margin | right_margin
| rotate | rotation_increment | num_rotations -> samplervalue


// brush properties
hue: "h" value -> hue
sat: "s" value -> sat
bright: "b" value -> bright
alpha: "a" value -> alpha
thick: "t" value -> thick
rough: "r" value -> rough
consistency: "q" value -> consistency

// sample properties
margin: "m" value -> margin
left_margin: "lm" value -> left_margin
right_margin: "rm" value -> right_margin
rotate: "rt" value -> rotate
rotation_increment: "ri" value -> rotation_increment
num_rotations: "nr" value -> num_rotations

// path properties
curve: "c" value -> curve
wavy:  "w" value -> wavy

// braces
rightbracecommand: RIGHTBRACE -> rightbrace

value: NUMBER | ("(" NUMBER "," NUMBER")") | variable [multiplier]
variable: "{" CNAME "}"
multiplier: "*" NUMBER

CANVASCOMMAND: "canvas"
STROKECOMMAND: "stroke"
BRUSHCOMMAND: "brush"
SAMPLECOMMAND: "sample"
SAVECOMMAND: "save"
LIGHTCOMMAND: "light"
COMMENT: "-" /[^\n]*/ NEWLINE
RIGHTBRACE: "]"

%import common.SIGNED_NUMBER -> NUMBER
%import common.NEWLINE -> NEWLINE
%import common.INTEGER -> INTEGER
%import common.CNAME -> CNAME
%import common.WS
%ignore WS