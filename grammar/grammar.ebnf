start: instruction+

instruction: canvas | stroke | brush | sample | COMMENT | rightbracecommand | save
stroke: STROKECOMMAND coordinate coordinate [curve] [wavy] -> stroke
brush: BRUSHCOMMAND (brushvalue)+  -> brush
canvas: CANVASCOMMAND value value value
save: SAVECOMMAND CNAME -> save

coordinate: value value  -> coordinate
brushvalue: hue | sat | bright | alpha | thick | rough -> brushvalue
sample: SAMPLECOMMAND value

// brush properties
hue: "h" value -> hue
sat: "s" value -> sat
bright: "b" value -> bright
alpha: "a" value -> alpha
thick: "t" value -> thick
rough: "r" value -> rough

// path properties
curve: "c" value -> curve
wavy:  "w" value -> wavy

// braces
rightbracecommand: RIGHTBRACE -> rightbrace

CANVASCOMMAND: "canvas"
STROKECOMMAND: "stroke"
BRUSHCOMMAND: "brush"
SAMPLECOMMAND: "sample"
SAVECOMMAND: "save"
COMMENT: "-" /[^\n]*/ NEWLINE
RIGHTBRACE: "]"
uniform: "u" -> uniform
normal: "n" -> normal
value: NUMBER | ((uniform | normal) NUMBER ":" NUMBER)



%import common.SIGNED_NUMBER -> NUMBER
%import common.NEWLINE -> NEWLINE
%import common.INTEGER -> INTEGER
%import common.CNAME -> CNAME
%import common.WS
%ignore WS